NEW
100 REM LRI EDITOR
110 DIM D(50):S=16384:V=16:W=32:R$="TVQRWUS":S$="        "
115 FOR I=1 TO 7:READ R(I):NEXT:DATA 32,48,96,0,80,16,64
200 CLS 0:GOSUB 410
205 C=1:M=4
210 POKE C(C),124:POKE M(M),126
215 P=POINT(X,Y):Q=P=0:R=PEEK(S+INT(Y/2)*W+X/2)/V-7
220 IF P THEN RESET(X,Y)
225 IF Q THEN SET(X,Y,R)
230 K$=INKEY$:K=ASC(K$+CHR$(0)):IF K=32 THEN 215
235 H=(K=65)-(K=83):I=(K=87)-(K=90):J=H OR I:IF J AND M=3 THEN 250
240 IF Q THEN RESET(X,Y)
245 IF P THEN SET(X,Y,P)
250 IF J AND M=1 THEN SET(X,Y,C)
255 IF J AND M=2 THEN RESET(X,Y)
260 X=X+H AND 31:Y=Y+I AND 31
265 IF K=13 THEN SET(X,Y,C)
270 IF K>48 AND K<56 THEN POKE C(C),96:C=K-48
275 N=-(K=68)-2*(K=69)-3*(K=73)-4*(K=77):IF N THEN POKE M(M),96:M=N
280 IF K=70 THEN 310
295 GOTO 210
300 PRINT @469,:INPUT N$:IF N$="" THEN 210
305 PRINT @469," ";:RETURN
310 PRINT @470,"LOAD/SAVE";
312 K$=INKEY$:IF K$="" THEN 312
314 PRINT @470," "S$;
316 IF K$="S" THEN GOSUB 300: GOTO 370
318 IF K$<>"L" THEN 210
320 GOSUB 300
325 CLOAD*D,N$
330 P=S:N=0:R=0:CLS 0:GOSUB 410
332 FOR I=0 TO 255:G=PEEK(VARPTR(D(0))+I):IF G=0 THEN 205
334 G=G-48:IF G<0 THEN 365
336 IF G<10 THEN N=G:GOTO 365
338 G=G-17:IF G<0 THEN 365
340 IF G>31 THEN G=G-32
342 IF G<16 THEN 350
344 G=G-15:IF G<8 THEN R=R(G)
346 GOTO 365
350 FOR J=1 TO N:POKE P,128+R+G:P=P+1:IF (P AND 15)=0 THEN P=P+V
355 IF P>S+511 THEN 205
360 NEXT:N=0
365 NEXT:GOTO 205
370 C=0:D=0:G=0:N=1:R=0:FOR I=0 TO 511 STEP W:FOR J=0 TO 15
372 P=PEEK(S+I+J):Q=INT(P/V)-7:P=PAND15:K=P+65
374 IF P AND Q<>R THEN GOSUB 390:Z=ASC(MID$(R$,Q)):GOSUB 398:R=Q:GOTO 385
376 IF K=G AND N<9 THEN N=N+1:GOTO 385
380 GOSUB 390
385 NEXT:K=0:GOSUB 390:NEXT:Z=0:GOSUB 398
388 CSAVE*D,N$:PRINT @470,"SAVED    ";:GOTO 210
390 IF G=0 THEN GOTO 396
392 IF N>1 THEN Z=48+N:GOSUB 398
394 Z=G:GOSUB 398:
396 G=K:N=1:RETURN 
398 POKE VARPTR(D(0))+D,Z:D=D+1:IF D<255 THEN RETURN
399 PRINT @470,"OVERFLOW";:GOTO 210
410 PRINT @16,"   LRI EDITOR   ";:PRINT @48,S$S$;
411 PRINT @80," ARROWS = MOVE  ";:PRINT @112,"  ENTER = SET   ";
412 PRINT @144," SPACE = TOGGLE ";:PRINT @176,S$S$;
413 PRINT @208,"  1 #    DRAW   ";:PRINT @240,"  2 #    ERASE  ";
414 PRINT @272,"  3 #    INVERT ";:PRINT @304,"  4 #    MOVE   ";
415 PRINT @336,"  5 #   "S$;:PRINT @368,"  6 #   "S$;
416 PRINT @400,"  7 #   "S$;:PRINT @432,S$S$;
417 PRINT @464," FILE   "S$;:PRINT @496,S$"       ";
418 POKE S+511,96
419 FOR I=1 TO 7:K=S+180+I*W:POKE K,127+I*V:C(I)=K+1:M(I)=K+4:NEXT
420 RETURN
RUN 
