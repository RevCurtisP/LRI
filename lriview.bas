100 REM 'LRI Image Viewer for PC-BASIC
110 DIM G$(15):FOR I=0 TO 15:READ GL,GR:G$(I)=CHR$(GL)+CHR$(GR):NEXT
112 DATA  32, 32,  32,220, 220,32, 220,220,  32,223,  32,219, 220,223, 220,219
114 DATA 223, 32, 223,220, 219,32, 219,220, 223,223, 223,219, 219,223, 219,219
116 FOR I=1 TO 7:READ F(I):NEXT:DATA 1,4,5,2,3,14,15
120 CLS
130 COLOR 7:INPUT "FILENAME";N$:IF N$="" THEN END
135 X$=RIGHT$(N$,4):IF X$<>".LRI" AND X$<>".lri" THEN N$=N$+".LRI"
140 D$="":REM 'Read LRI File
145 OPEN "I",1,N$
150 IF EOF(1) THEN 170
155 INPUT#1,L$
160 D$=D$+L$
165 GOTO 150
170 CLOSE 1
175 IF D$="" THEN END
200 REM 'Decode and Display Image
205 CLS:COLOR F(7):PRINT "    ";
210 FOR I=1 TO LEN(D$)
215 G=ASC(MID$(D$,I))-48:IF G<0 THEN 290
220 IF G<10 THEN N=G:GOTO 290
230 G=G-17:IF G<0 THEN 290
240 G=G+32*(G>31):IF G<16 THEN 260
250 G=G-15:IF G<8 THEN COLOR F(G)
255 GOTO 290
260 FOR J=1 TO N-(N=0):PRINT G$(G);
275 C=C+1:IF C=16 THEN PRINT:PRINT "    ";:C=0
280 NEXT:N=0
290 NEXT
295 PRINT:PRINT:PRINT:PRINT:GOTO 130
