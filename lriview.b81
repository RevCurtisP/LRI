 100 REM ZX81 LRI VIEWER
 110 LET F=0
 190 GOSUB 600
 200 REM DECODE/DISPLAY IMAGE
 205 IF F THEN FAST 
 210 LET F$=" \ .\. \..\ '\ :\.'\.:\' \'.\: \:.\''\':\:'% "
 215 LET G=0
 220 LET N=1
 225 LET C=0
 230 LET L=2
 280 CLS 
 290 PRINT AT L,8;
 300 REM LOOP THROUGH DATA
 305 LET D=LEN D$
 310 FOR I=1 TO D
 312 LET G=CODE  D$(I)
 314 IF G>128 THEN LET G=G-128
 316 LET G=G-28
 318 IF G<0 THEN GOTO 390
 320 IF G>9 THEN GOTO 330
 322 LET N=G
 324 IF N=0 THEN N=1
 328 GOTO 390
 330 LET G=G-9
 335 IF G>16 THEN GOTO 390
 345 LET G$=F$(G)
 350 FOR J=1 TO N
 355 PRINT G$;
 360 LET C=C+1
 365 IF C<16 THEN GOTO 380
 370 LET C=0
 372 LET L=L+1
 374 PRINT AT L,8;
 380 NEXT J
 385 LET N=1
 390 NEXT I
 400 REM WAIT FOR KEYPRESS
 410 IF INKEY$="" THEN GOTO 410
 420 GOTO 190
 600 REM USER INTERFACE
 605 SLOW 
 610 CLS 
 612 LET F$="FASTSLOW"
 614 GOSUB 800
 616 PRINT AT 20,6;", Q=QUIT",,"ENTER IMAGE NUMBER"
 618 LET D$=""
 620 LET G=F+1
 625 PRINT AT 20,0;F$(G);"=";F$(G TO G+3)
 630 LET G$=INKEY$
 632 IF G$="" THEN GOTO 630
 634 IF G$="Q" THEN GOTO 999
 636 IF G$="F" THEN LET F=4
 638 IF G$="S" THEN LET F=0
 640 LET G=CODE G$
 642 LET N=LEN D$
 644 IF G=118 AND N THEN GOTO 680
 650 IF G<>119 THEN GOTO 660
 652 IF N=0 THEN GOTO 630
 654 LET D$=D$(1 TO N-1)
 656 GOTO 666
 660 IF G$<"0" OR G$>"9" THEN GOTO 620 
 662 IF N>1 THEN GOTO 630
 664 LET D$=D$+G$
 666 PRINT AT 21,19;D$;"\##  ";
 668 GOTO 630
 680 LET G=VAL D$
 682 LET D$=""
 684 IF G THEN GOSUB 900+G*2
 686 IF D$="" THEN GOTO 656
 690 RETURN 
 800 REM DISPLAY IMAGE NAMES
 801 PRINT " 1 ATOM"
 802 PRINT " 2 AVENGERS"
 803 PRINT " 3 DNA"
 804 PRINT " 4 RUBIK"
 805 PRINT " 5 TARDIS"
 806 PRINT " 6 RABBIT"
 807 PRINT " 7 ROBOT"
 890 RETURN 
 900 REM RLI IMAGE DATA
 902 LET D$="V6ABPPC6A5ABOAANC5A4ABO4ANC4AUAH9PVPUPPLAHIAAVPTB4DCVPUAAELP3AVPTO4MNVPU3APELATBVPAASBCAAVPTCAUHIAUELTOVPASBPPCAVPTNUHIAATBOULVPASEPPIAVPUHTNCABOAUEVPAASEIAAVPUITANCP3AVPUL4DHVPT3APNCAAVPUE4MIVPTAABOANPPVPT9POA4AVEPCAABPI4A5AEPCBPI5A6AEPPI6A"
 903 RETURN 
 904 LET D$="Q8ABPPK4A8AHPPK4A4ABD5PK4A3ABPMIH4PC3AAABPIAB4POPCAAABPIAAHPOPPKEPCAAFKAABPPIPPKAFKAAPIAAHPOAPPKAEPAAPAABPPIANPKAAPAAPAAHPOAALNKAAPAAPCB5PPLIABPAAFKHPO3MPJKAFKAAEJPPI3AJPKBPIAAAHPO4AMMJPIAAABPPJDCAABDPI3AAHPOEM4POI4A"
 905 RETURN
 906 LET D$="5AvJrMMtMMvG5A6AMDDM6A5ABMAAMC5A4ABIuMMsMMvEC4A4AKt3Mr3MvF4A4AJs3Mu3MvG4A5AvJrMMtMMvG5A6AMDDM6A5ABMAAMC5A4ABIuMMsMMvEC4A4AKt3Mr3MvF4A4AJs3Mu3MvG4A5AvJrMMtMMvG5A6AMDDM6A5ABMAAMC5A4ABIuMMsMMvEC4A"
 907 RETURN
 908 LET D$="8A8A8A8A5ArEMMtEMMwEMMvCA4AwEMMsEMMwEMMsCvKA3AwEMMrEMMvEMMqCsKvKA3AvPPKqPPKrPPKqKsKsCA3AvPPKqPPKrPPKqKqCsKA3AvMMIqMMIrMMItCqKsKA3ArPPKvPPKsPPKtKqKvCA3ArPPKvPPKsPPKtKwCvKA3ArMMIvMMIsMMIsCwKvKA3AtPPKsPPKqPPKsKwKAA3AtPPKsPPKqPPKsK3A3AtMMIsMMIqMMI4A8A8A8A8A"
 909 RETURN
 910 LET D$="u7AHL7Aq4ABPPPPPPC4A3ABO6MNC3A4APMMNOMMP4A4APFPFKPKP4A4APEMFKMIP4A4APMMNOMMP4A4APFPFKPKP4A4APEMFKMIP4A4APMMNOMMP4A4APFPFKPKP4A4APEMFKMIP4A4APMMNOMMP4A4APFPFKPKP4A4APEMFKMIP4A3AB8PC3A"
 911 RETURN
 912 LET D$="w5A3DC7A4AB4PC6A4AFLDDNPC5A3ABP3MLNPPLCAA3AEPCAAE5PCA4AN4PHPKFPLA5AEMMH6PA6ADDH5PIA4AD4PG3PIAA3AF6PLDPKAAAAB4PM5PKAAABG5PF4PIAAAPF5PKPOHK3AANLN3POHPPLPCAA3AENPLMINPOMIAA8A8A"
 913 RETURN
 914 LET D$="4AqJuFMPPMKqG4A5AuPDLHDPArBIEC5AuFPDDPKArEDDI7AqPP4ANOA4ArBuO4MNrCAqHKAAADPrPuKvPtPsPwPuFrPqPOAAAFOArEuL4DHrI4AAqHLAAuPNMMOP5ArBMMCAu6P5ArECBIAqN4PO5A5ArEPIEPI5A6AuPAAP6A5ABPCBPC5A6APAAP6A6APAAP6A5ArBPCBPC5A"
 998 RETURN 
 
 999 REM END OF PROGRAM
